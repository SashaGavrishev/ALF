!  Copyright (C) 2016 - 2022 The ALF project
!
!  This file is part of the ALF project.
!
!     The ALF project is free software: you can redistribute it and/or modify
!     it under the terms of the GNU General Public License as published by
!     the Free Software Foundation, either version 3 of the License, or
!     (at your option) any later version.
!
!     The ALF project is distributed in the hope that it will be useful,
!     but WITHOUT ANY WARRANTY; without even the implied warranty of
!     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!     GNU General Public License for more details.
!
!     You should have received a copy of the GNU General Public License
!     along with ALF. If not, see http://www.gnu.org/licenses/.
!
!     Under Section 7 of GPL version 3 we require you to fulfill the following additional terms:
!
!     - It is our hope that this program makes a contribution to the scientific community. Being
!       part of that community we feel that it is reasonable to require you to give an attribution
!       back to the original authors if you have benefitted from this program.
!       Guidelines for a proper citation can be found on the project's homepage
!       http://alf.physik.uni-wuerzburg.de .
!
!     - We require the preservation of the above copyright notice and this license in all original files.
!
!     - We prohibit the misrepresentation of the origin of the original source files. To obtain
!       the original source files please visit the homepage http://alf.physik.uni-wuerzburg.de .
!
!     - If you make substantial changes to the program we require you to either consider contributing
!       to the ALF project or to mark your material in a reasonable way as different from the original version.


!--------------------------------------------------------------------
!> @author
!> ALF-project
!>
!> @brief
!> This module handles the  calculation of the acceptance ratio.
!> It also monitors the precision of the code, as well as the timing.
!
!--------------------------------------------------------------------


module Control

    use files_mod
    Use MyMats
    use iso_fortran_env, only: output_unit, error_unit
    use Instrument_mod, only: instrument_on
    Implicit none

    real    (Kind=Kind(0.d0)), private, save :: XMEANG, XMAXG, XMAXP,  Xmean_tau, Xmax_tau
    Integer (Kind=Kind(0.d0)), private, save :: count_CPU_start,count_CPU_end,count_rate,count_max
    Integer          , private, save :: NCG, NCG_tau
    Integer (Kind=Kind(0.d0)) , private, save :: NC_up, ACC_up
    Integer (Kind=Kind(0.d0)) , private, save :: NC_eff_up, ACC_eff_up

    ! How much of the run is spent applying the low-rank update to the Green's
    ! function -- the block upgrade_mod applies on every accepted move, and the
    ! only part a delayed (rank-k) scheme would replace. Reported as a share of
    ! CPU Time, because that share is what bounds what such a scheme could ever
    ! be worth: a kernel made S times faster on a fraction F of the runtime
    ! returns 1/((1-F) + F/S), which is capped at 1/(1-F) however good S is.
    !
    ! Sampled rather than timed on every update; see upgrade_mod. NC_update
    ! counts every update applied, NC_update_timed only those a clock was read
    ! around, so the estimate is Time_update * NC_update / NC_update_timed.
    Real    (Kind=Kind(0.d0)), private, save :: Time_update
    Integer (Kind=Kind(0.d0)), private, save :: NC_update, NC_update_timed
    ! How much of the run is spent applying the hopping propagator e^{-dtau T}
    ! to a full matrix -- the other candidate for replacement beside the update
    ! above, and the one a checkerboard decomposition or a momentum-space
    ! transform would change. Bounded by the same Amdahl argument, so it is
    ! reported the same way and beside it.
    !
    ! Timed on every application rather than on a sample; see Hop_mod for why
    ! the two brackets differ. NC_hop therefore normally equals NC_hop_timed,
    ! and a gap between them means intervals were dropped for a wrapped clock.
    Real    (Kind=Kind(0.d0)), private, save :: Time_hop
    Integer (Kind=Kind(0.d0)), private, save :: NC_hop, NC_hop_timed

    ! How many Metropolis decisions were close enough to their threshold that a
    ! last-bit difference could have flipped them.
    !
    ! A delayed (rank-k) update reassociates the arithmetic without consuming a
    ! single extra random number, so it replays the *same* Markov chain unless a
    ! proposal sits within rounding of `Weight > tmp_r`. That makes a trajectory
    ! comparison between the two schemes a sharp test -- but only if a divergence
    ! can be told apart from a wrong update. This counter is what tells them
    ! apart: a chain that diverged with zero near ties took a different decision,
    ! which is a bug, not reassociation.
    !
    ! Rank-local like the other counts; a sum across ranks beside a mean would
    ! reconstruct nothing.
    Integer (Kind=Kind(0.d0)), private, save :: NC_near_tie
    ! Delay depth in force, 0 when the delayed update is off. Recorded because
    ! ALF_DELAY_K is an environment variable rather than a parameter, and a knob
    ! that changes the arithmetic must leave a trace in the run record.
    Integer, private, save :: Delay_depth_used = 0

    ! How that depth was arrived at: fixed by ALF_DELAY_K, measured by the probe
    ! "auto" runs, or the closed form the probe falls back to. Reported on its own
    ! line rather than folded into the depth, because the fallback is the failure
    ! worth seeing -- a probe that quietly fell back on every chain is a probe
    ! that is not running, and a depth alone cannot show it.
    Character (Len=16), private, save :: Delay_depth_from = 'off'
    Integer (Kind=kind(0.d0)),  private, save :: NC_Glob_up, ACC_Glob_up
    Integer (Kind=kind(0.d0)),  private, save :: NC_HMC_up, ACC_HMC_up
    Integer (Kind=kind(0.d0)),  private, save :: NC_Temp_up, ACC_Temp_up
    real    (Kind=Kind(0.d0)),  private, save :: XMAXP_Glob, XMEANP_Glob
    Integer (Kind=Kind(0.d0)),  private, save :: NC_Phase_GLob

    real    (Kind=Kind(0.d0)),  private, save :: XMAXP_HMC, XMEANP_HMC
    Integer (Kind=Kind(0.d0)),  private, save :: NC_Phase_HMC

    
    real    (Kind=Kind(0.d0)),  private, save :: size_clust_Glob_up, size_clust_Glob_ACC_up

    real    (Kind=Kind(0.d0)),  private, save :: Force_max, Force_mean
    Integer, private, save  :: Force_Count
#ifdef MPI
    Integer                  ,  private, save :: Ierr, Isize, Irank, irank_g, isize_g, igroup
#endif

    
    Contains

      subroutine control_init(Group_Comm)
#ifdef MPI
        Use mpi
#endif
        Implicit none
        Integer       , INTENT(IN)               :: Group_Comm

        XMEANG     = 0.d0
        XMEAN_tau  = 0.d0
        XMAXG      = 0.d0
        XMAX_tau   = 0.d0
        XMAXP      = 0.d0
        XMEANP_Glob= 0.d0
        XMAXP_Glob = 0.d0
        XMEANP_HMC = 0.d0
        XMAXP_HMC  = 0.d0

        
        NCG          = 0
        NCG_tau      = 0
        NC_up        = 0
        ACC_up       = 0
        NC_eff_up    = 0
        ACC_eff_up   = 0
        NC_Glob_up   = 0
        ACC_Glob_up  = 0
        NC_Phase_GLob= 0

        NC_Phase_HMC = 0
        NC_HMC_up    = 0
        ACC_HMC_up   = 0
        
        NC_Temp_up   = 0
        ACC_Temp_up  = 0


        Time_update     = 0.d0
        NC_update       = 0
        NC_update_timed = 0

        Time_hop        = 0.d0
        NC_hop          = 0
        NC_hop_timed    = 0

        NC_near_tie     = 0

        size_clust_Glob_up    = 0.d0
        size_clust_Glob_ACC_up= 0.d0

        Force_max  = 0.d0
        Force_mean = 0.d0
        Force_count = 0
        
#ifdef MPI
        CALL MPI_COMM_SIZE(MPI_COMM_WORLD,ISIZE,IERR)
        CALL MPI_COMM_RANK(MPI_COMM_WORLD,IRANK,IERR)
        call MPI_Comm_rank(Group_Comm, irank_g, ierr)
        call MPI_Comm_size(Group_Comm, isize_g, ierr)
        igroup           = irank/isize_g
#endif
        
        call system_clock(count_CPU_start,count_rate,count_max)
      end subroutine control_init


!-------------------------------------------------------------

      Subroutine Control_Langevin(Forces, Group_Comm)


        Implicit none
        
        Complex (Kind=Kind(0.d0)), Intent(In)  :: Forces(:,:)
        Integer, Intent(IN) :: Group_Comm
        
        Integer :: n1,n2, n, nt 
        Real (Kind = Kind(0.d0) ) :: X

        ! Test for not a  number
        n1 =  size(Forces,1)
        n2 =  size(Forces,2)
        Force_count =  Force_count  + 1

        X = 0.d0
        do  n = 1,n1
           do nt =1,n2
              If ( abs( Real(Forces(n,nt),kind(0.d0))) >=  Force_max  ) &
                   &  Force_max = abs( Real(Forces(n,nt),kind(0.d0)))
              X  = X + abs( Real(Forces(n,nt),kind(0.d0)) )
           enddo
        enddo
        Force_mean = Force_mean  +  X/Real(n1*n2,Kind(0.d0)) 
        
      end Subroutine Control_Langevin

      
      Subroutine Control_upgrade(toggle)
        Implicit none
        Logical :: toggle
        NC_up = NC_up + 1
        if (toggle) ACC_up = ACC_up + 1
      end Subroutine Control_upgrade

      Subroutine Control_upgrade_eff(toggle)
        Implicit none
        Logical :: toggle
        NC_eff_up = NC_eff_up + 1
        if (toggle) ACC_eff_up = ACC_eff_up + 1
      end Subroutine Control_upgrade_eff

!--------------------------------------------------------------------
!> @brief
!> Record that one low-rank Green's-function update was applied, and -- when
!> upgrade_mod sampled this one -- how long it took.
!>
!> @details
!> Counted for every accepted move in either mode, not only FINAL ones: an
!> INTERMEDIATE leg of a composite move applies the same update and costs the
!> same, so excluding it (as Control_upgrade does, correctly, for the acceptance
!> *rate*) would understate the share of runtime this block owns.
!>
!> ``seconds`` is ignored unless ``timed``, which keeps the caller free to read
!> the clock on only a sample of updates.
!--------------------------------------------------------------------
      Subroutine Control_update(timed, seconds)
        Implicit none
        Logical, Intent(In) :: timed
        Real (Kind=Kind(0.d0)), Intent(In) :: seconds
        NC_update = NC_update + 1
        if (timed) then
           Time_update     = Time_update + seconds
           NC_update_timed = NC_update_timed + 1
        endif
      end Subroutine Control_update

!--------------------------------------------------------------------
!> @brief
!> Book one Metropolis decision that was within `tol` of its threshold.
!> @details
!> See NC_near_tie. The caller decides what "close" means; this only counts.
!--------------------------------------------------------------------
      Subroutine Control_near_tie()
        Implicit none
        NC_near_tie = NC_near_tie + 1
      end Subroutine Control_near_tie

!--------------------------------------------------------------------
!> @brief
!> Record the delay depth this run is using, for the info file.
!> @details
!> Set once at setup, not per update, and deliberately *not* cleared by
!> Control_init: the depth is a property of the build and environment, and
!> Control_init runs again just before the bin loop.
!--------------------------------------------------------------------
      Subroutine Control_set_delay_depth(k, source)
        Implicit none
        Integer, Intent(In) :: k
        Character (Len=*), Intent(In), Optional :: source
        Delay_depth_used = k
        if (Present(source)) Delay_depth_from = source
      end Subroutine Control_set_delay_depth

!--------------------------------------------------------------------
!> @brief
!> Book one application of the hopping propagator, timed or not.
!> @details
!> One call is one bracketed region in Hop_mod, which is one propagation of a
!> full matrix -- except Hop_mod_Symm, whose region carries the two-sided
!> adjoint action and so contains two multiplications. The share is unaffected;
!> only NC_hop reads low against a per-multiplication count.
!--------------------------------------------------------------------
      Subroutine Control_hop(timed, seconds)
        Implicit none
        Logical, Intent(In) :: timed
        Real (Kind=Kind(0.d0)), Intent(In) :: seconds
        NC_hop = NC_hop + 1
        if (timed) then
           Time_hop     = Time_hop + seconds
           NC_hop_timed = NC_hop_timed + 1
        endif
      end Subroutine Control_hop

      Subroutine Control_upgrade_Temp(toggle)
        Implicit none
        Logical :: toggle
        NC_Temp_up  =  NC_Temp_up    +  1
        if (toggle)  ACC_Temp_up  = ACC_Temp_up + 1
      end Subroutine Control_upgrade_Temp

      Subroutine Control_upgrade_Glob(toggle,size_clust)
        Implicit none
        Logical :: toggle
        Real (Kind=Kind(0.d0)), intent(in) :: size_clust
        NC_Glob_up = NC_Glob_up + 1
        size_clust_Glob_up = size_clust_Glob_up + size_clust
        if (toggle) then
          ACC_Glob_up = ACC_Glob_up + 1
          size_clust_Glob_ACC_up = size_clust_Glob_ACC_up + size_clust
        endif
      end Subroutine Control_upgrade_Glob

      Subroutine Control_upgrade_HMC(toggle)
        Implicit none
        Logical :: toggle
        NC_HMC_up = NC_HMC_up + 1
        if (toggle) then
           ACC_HMC_up = ACC_HMC_up + 1
        endif
      end Subroutine Control_upgrade_HMC


      Subroutine Control_PrecisionG(A,B,Ndim)
#ifdef MPI
        Use mpi
#endif
        use runtime_error_mod
        Implicit none

        Integer :: Ndim
        Complex (Kind=Kind(0.d0)) :: A(Ndim,Ndim), B(Ndim,Ndim)
        Real    (Kind=Kind(0.d0)) :: XMAX, XMEAN
        Character (len=64) :: file1 

        if (any(A /= A)) then
#if defined(TEMPERING) 
          write(File1,'(A,I0,A)') "Temp_",igroup,"/info"
#else
          File1 = "info"
#endif
          Open (Unit=50,file=file1, status="unknown", position="append")
          write(50,*)
#ifdef MPI
          write(50,*) "Task", Irank_g, "of group", igroup, "reports:"
#endif
          write(50,*) "Green function A contains NaN, calculation is being aborted!"
          write(50,*)
          close(50)
          write(error_unit,*)
#ifdef MPI
          write(error_unit,*) "Task", Irank_g, "of group", igroup, "reports:"
#endif
          write(error_unit,*) "Green function A contains NaN, calculation is being aborted!"
          write(error_unit,*)
          CALL Terminate_on_error(ERROR_GENERIC,__FILE__,__LINE__)
        endif

        if (any(B /= B)) then
#if defined(TEMPERING) 
          write(File1,'(A,I0,A)') "Temp_",igroup,"/info"
#else
          File1 = "info"
#endif
          Open (Unit=50,file=file1, status="unknown", position="append")
          write(50,*)
#ifdef MPI
          write(50,*) "Task", Irank_g, "of group", igroup, "reports:"
#endif
          write(50,*) "Green function B contains NaN, calculation is being aborted!"
          write(50,*)
          close(50)
          write(error_unit,*)
#ifdef MPI
          write(error_unit,*) "Task", Irank_g, "of group", igroup, "reports:"
#endif
          write(error_unit,*) "Green function B contains NaN, calculation is being aborted!"
          write(error_unit,*)
          CALL Terminate_on_error(ERROR_GENERIC,__FILE__,__LINE__)
        endif

        NCG = NCG + 1
        CALL COMPARE(A, B, XMAX, XMEAN)
        IF (XMAX  >  10.d0) then
#if defined(TEMPERING) 
          write(File1,'(A,I0,A)') "Temp_",igroup,"/info"
#else
          File1 = "info"
#endif
          Open (Unit=50,file=file1, status="unknown", position="append")
          write(50,*)
#ifdef MPI
          write(50,*) "Task", Irank_g, "of group", igroup, "reports:"
#endif
          write(50,*) XMAX, " is exceeding the threshold of 10 for G difference!"
          write(50,*) (XmeanG+Xmean)/ncg, " is the average deviation!"
          write(50,*) "This calculation is unstable and therefore aborted!!!"
          write(50,*)
          close(50)
          write(error_unit,*)
#ifdef MPI
          write(error_unit,*) "Task", Irank_g, "of group", igroup, "reports:"
#endif
          write(error_unit,*) XMAX, " is exceeding the threshold of 10 for G difference!"
          write(error_unit,*) (XmeanG+Xmean)/ncg, " is the average deviation!"
          write(error_unit,*) "This calculation is unstable and therefore aborted!!!"
          write(error_unit,*) 'Try with smaller Nwrap or dtau.'
          write(error_unit,*)
          
          CALL Terminate_on_error(ERROR_UNSTABLE_MATRIX,__FILE__,__LINE__)
          
        endif
        IF (XMAX  >  XMAXG) XMAXG = XMAX
        XMEANG = XMEANG + XMEAN
      End Subroutine Control_PrecisionG

      Subroutine Control_Precision_tau(A,B,Ndim)
        Implicit none

        Integer :: Ndim
        Complex (Kind=Kind(0.d0)) :: A(Ndim,Ndim), B(Ndim,Ndim)
        Real    (Kind=Kind(0.d0)) :: XMAX, XMEAN

        NCG_tau = NCG_tau + 1
        CALL COMPARE(A, B, XMAX, XMEAN)
        IF (XMAX  >  XMAX_tau) XMAX_tau = XMAX
        XMEAN_tau = XMEAN_tau + XMEAN
      End Subroutine Control_Precision_tau


      Subroutine Control_PrecisionP(Z,Z1)
        Implicit none
        Complex (Kind=Kind(0.D0)), INTENT(IN) :: Z,Z1
        Real    (Kind=Kind(0.D0)) :: X
        X = ABS(Z-Z1)
        if ( X > XMAXP ) XMAXP = X
      End Subroutine Control_PrecisionP


      Subroutine Control_PrecisionP_Glob(Z,Z1)
        Implicit none
        Complex (Kind=Kind(0.D0)), INTENT(IN) :: Z,Z1
        Real    (Kind=Kind(0.D0)) :: X
        X = ABS(Z-Z1)
        if ( X > XMAXP_Glob ) XMAXP_Glob = X
        XMEANP_Glob = XMEANP_Glob + X
        NC_Phase_GLob = NC_Phase_GLob + 1
      End Subroutine Control_PrecisionP_Glob

      Subroutine Control_PrecisionP_HMC(Z,Z1)
        Implicit none
        Complex (Kind=Kind(0.D0)), INTENT(IN) :: Z,Z1
        Real    (Kind=Kind(0.D0)) :: X
        X = ABS(Z-Z1)
        if ( X > XMAXP_HMC ) XMAXP_HMC = X
        XMEANP_HMC = XMEANP_HMC + X
        NC_Phase_HMC = NC_Phase_HMC + 1
      End Subroutine Control_PrecisionP_HMC


      Subroutine Control_Print(Group_Comm, Global_update_scheme)
#ifdef MPI
        Use mpi
#endif
        Implicit none

        Integer, Intent(IN) :: Group_Comm
        Character (Len = 64), Intent(IN) :: Global_update_scheme
                

        Character (len=64) :: file1
        Real (Kind=Kind(0.d0)) :: Time, Acc, Acc_eff, Acc_Glob, Acc_Temp, size_clust_Glob, size_clust_Glob_ACC, Acc_HMC
        Real (Kind=Kind(0.d0)) :: Update_time, Update_share, Hop_time, Hop_share
#ifdef MPI
        REAL (Kind=Kind(0.d0))  :: X
        Integer        :: Ierr, Isize, Irank, irank_g, isize_g, igroup

        CALL MPI_COMM_SIZE(MPI_COMM_WORLD,ISIZE,IERR)
        CALL MPI_COMM_RANK(MPI_COMM_WORLD,IRANK,IERR)
        call MPI_Comm_rank(Group_Comm, irank_g, ierr)
        call MPI_Comm_size(Group_Comm, isize_g, ierr)
        igroup           = irank/isize_g
#endif

        ACC = 0.d0
        IF (NC_up > 0 )  ACC = dble(ACC_up)/dble(NC_up)
        ACC_eff = 0.d0
        IF (NC_eff_up > 0 )  ACC_eff = dble(ACC_eff_up)/dble(NC_eff_up)
        ACC_Glob = 0.d0
        size_clust_Glob = 0.d0
        size_clust_Glob_ACC = 0.d0
        IF (NC_Glob_up    > 0 )  then
          ACC_Glob    = dble(ACC_Glob_up)/dble(NC_Glob_up)
          size_clust_Glob     = size_clust_Glob_up     / dble( NC_Glob_up)
          size_clust_Glob_ACC = size_clust_Glob_ACC_up / dble(ACC_Glob_up)
        endif
        ACC_HMC = 0.d0
        IF (NC_HMC_up    > 0 )  then
           ACC_HMC    = dble(ACC_HMC_up)/dble(NC_HMC_up)
        endif

        
        ACC_TEMP = 0.d0
        IF (NC_Temp_up    > 0 )  ACC_Temp    = dble(ACC_Temp_up)/dble(NC_Temp_up)
        IF (NC_Phase_GLob > 0 ) XMEANP_Glob  = XMEANP_Glob/dble(NC_Phase_GLob)

        call system_clock(count_CPU_end)
        time = (count_CPU_end-count_CPU_start)/dble(count_rate)
        if (count_CPU_end .lt. count_CPU_start) time = (count_max+count_CPU_end-count_CPU_start)/dble(count_rate)

        ! Scale the sampled updates up to all of them. Both are zero for a run
        ! that accepted nothing, which is reported as no verdict rather than as
        ! a zero share.
        Update_time  = 0.d0
        Update_share = 0.d0
        if (NC_update_timed > 0) then
           Update_time = Time_update * dble(NC_update)/dble(NC_update_timed)
           if (Time > 0.d0) Update_share = Update_time/Time
        endif

        ! Same scale-up as above, which is the identity while every application
        ! is timed. It is written out anyway so that dropped intervals -- or a
        ! future decision to sample this bracket too -- need no change here.
        Hop_time  = 0.d0
        Hop_share = 0.d0
        if (NC_hop_timed > 0) then
           Hop_time = Time_hop * dble(NC_hop)/dble(NC_hop_timed)
           if (Time > 0.d0) Hop_share = Hop_time/Time
        endif

        If (str_to_upper(Global_update_scheme) == "LANGEVIN") Force_mean =  Force_mean/real(Force_count,kind(0.d0)) 
        
#if defined(MPI)
        If (str_to_upper(Global_update_scheme) == "LANGEVIN")  then
           X = 0.d0
           CALL MPI_REDUCE(Force_mean,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
           Force_mean= X/dble(Isize_g)
           CALL MPI_REDUCE(Force_max,X,1,MPI_REAL8,MPI_MAX, 0,Group_Comm,IERR)
           Force_max= X
        endif
        X = 0.d0
        CALL MPI_REDUCE(ACC,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        ACC = X/dble(Isize_g)
        X = 0.d0
        CALL MPI_REDUCE(ACC_eff,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        ACC_eff = X/dble(Isize_g)
        ! Means, as for Time and the acceptances: every rank does the same
        ! amount of this work, so a mean describes any one of them. The counts
        ! stay rank-local, since a summed count beside a mean time would not
        ! reconstruct anything.
        X = 0.d0
        CALL MPI_REDUCE(Update_time,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        Update_time = X/dble(Isize_g)
        X = 0.d0
        CALL MPI_REDUCE(Update_share,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        Update_share = X/dble(Isize_g)
        X = 0.d0
        CALL MPI_REDUCE(Hop_time,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        Hop_time = X/dble(Isize_g)
        X = 0.d0
        CALL MPI_REDUCE(Hop_share,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        Hop_share = X/dble(Isize_g)
        X = 0.d0
        CALL MPI_REDUCE(ACC_Glob,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        ACC_Glob = X/dble(Isize_g)
        X = 0.d0
        CALL MPI_REDUCE(ACC_HMC,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        ACC_HMC = X/dble(Isize_g)
        X = 0.d0
        CALL MPI_REDUCE(ACC_Temp ,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        ACC_Temp  = X/dble(Isize_g)

        X = 0.d0
        CALL MPI_REDUCE(size_clust_Glob,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        size_clust_Glob = X/dble(Isize_g)
        X = 0.d0
        CALL MPI_REDUCE(size_clust_Glob_ACC,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        size_clust_Glob_ACC = X/dble(Isize_g)

        X = 0.d0
        CALL MPI_REDUCE(XMEANG,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        XMEANG = X/dble(Isize_g)
        X = 0.d0
        CALL MPI_REDUCE(XMEAN_tau,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        XMEAN_tau = X/dble(Isize_g)

        X = 0.d0
        CALL MPI_REDUCE(XMEANP_Glob,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        XMEANP_Glob = X/dble(Isize_g)

        X = 0.d0
        CALL MPI_REDUCE(Time,X,1,MPI_REAL8,MPI_SUM, 0,Group_Comm,IERR)
        Time = X/dble(Isize_g)


        CALL MPI_REDUCE(XMAXG,X,1,MPI_REAL8,MPI_MAX, 0,Group_Comm,IERR)
        XMAXG = X
        CALL MPI_REDUCE(XMAX_tau,X,1,MPI_REAL8,MPI_MAX, 0,Group_Comm,IERR)
        XMAX_tau= X

        CALL MPI_REDUCE(XMAXP,X,1,MPI_REAL8,MPI_MAX, 0,Group_Comm,IERR)
        XMAXP = X

        CALL MPI_REDUCE(XMAXP_GLOB,X,1,MPI_REAL8,MPI_MAX, 0,Group_Comm,IERR)
        XMAXP_GLOB = X

        CALL MPI_REDUCE(XMAXP_HMC,X,1,MPI_REAL8,MPI_MAX, 0,Group_Comm,IERR)
        XMAXP_HMC = X

#endif

#if defined(TEMPERING)
        write(File1,'(A,I0,A)') "Temp_",igroup,"/info"
#else
        File1 = "info"
#endif

#if defined(MPI)
        If (Irank_g == 0 ) then
#endif

           Open (Unit=50,file=file1, status="unknown", position="append")
           If (NCG > 0 ) then
              XMEANG = XMEANG/dble(NCG)
              Write(50,*) ' Precision Green  Mean, Max : ', XMEANG, XMAXG
              Write(50,*) ' Precision Phase, Max       : ', XMAXP
           endif
           If ( NCG_tau > 0 ) then
              XMEAN_tau = XMEAN_tau/dble(NCG_tau)
              Write(50,*) ' Precision tau    Mean, Max : ', XMEAN_tau, XMAX_tau
           endif
           If ( NC_up > 0 ) then
              Write(50,*) ' Acceptance                 : ', ACC
           Endif
           If ( NC_eff_up > 0 ) then
              Write(50,*) ' Effective Acceptance       : ', ACC_eff
           Endif
           ! Always written, including the 0 of a run with the delay off: a
           ! missing line and a line reading 0 are the same fact, but only the
           ! second one distinguishes an old binary from a new one running
           ! immediate updates.
           If ( NC_update_timed > 0 ) then
              Write(50,*) ' Green updates              : ', NC_update
              Write(50,*) ' Green update time          : ', Update_time
              Write(50,*) ' Green update share         : ', Update_share
              Write(50,*) ' Green updates timed        : ', NC_update_timed
           Endif
           If ( NC_hop_timed > 0 ) then
              Write(50,*) ' Hopping applications       : ', NC_hop
              Write(50,*) ' Hopping time               : ', Hop_time
              Write(50,*) ' Hopping share              : ', Hop_share
           Endif
           Write(50,*) ' Delay depth                : ', Delay_depth_used
           Write(50,*) ' Delay depth from           : ', Trim(Delay_depth_from)
#ifdef ALF_INSTRUMENT
           ! Interprets a divergence between two builds as reassociation rather
           ! than as a bug: a proposal within rounding of its threshold can be
           ! decided either way by a last-bit difference.
           !
           ! Two gates, and they are not redundant. The macro decides whether the
           ! hoist that feeds this counter exists at all, which is what governs
           ! bitwise agreement with a stock build. instrument_on decides whether
           ! the run reports it: with the macro compiled in but ALF_INSTRUMENT
           ! unset the count is real, but a stock build must add nothing to info.
           if (instrument_on()) &
                Write(50,*) ' Metropolis near ties       : ', NC_near_tie
#endif

#if defined(TEMPERING)
           Write(50,*) ' Acceptance Tempering       : ', ACC_Temp
#endif
           If (ACC_Glob > 1.D-200 ) then
              Write(50,*) ' Acceptance_Glob              : ', ACC_Glob
              Write(50,*) ' Mean Phase diff Glob         : ', XMEANP_Glob
              Write(50,*) ' Max  Phase diff Glob         : ', XMAXP_Glob
              Write(50,*) ' Average cluster size         : ', size_clust_Glob
              Write(50,*) ' Average accepted cluster size: ', size_clust_Glob_ACC
           endif
           if (str_to_upper(Global_update_scheme) == "LANGEVIN") &
                &  Write(50,*) ' Langevin         Mean, Max : ', Force_mean,  Force_max
           
           if (str_to_upper(Global_update_scheme) == "HMC")   Then
              Write(50,*) ' Acceptance_HMC              : ', ACC_HMC
              Write(50,*) ' Mean Phase diff HMC         : ', XMEANP_HMC
              Write(50,*) ' Max  Phase diff HMC         : ', XMAXP_HMC
           Endif
           
           Write(50,*) ' CPU Time                   : ', Time
           Close(50)
#if defined(MPI)
        endif
#endif

      end Subroutine Control_Print


      subroutine make_truncation(prog_truncation,cpu_max,count_bin_start,count_bin_end, group_comm)
      !!!!!!! Written by M. Bercx, edited by J. Schwab
      ! This subroutine checks if the conditions for a controlled termination of the program are met.
      ! The subroutine contains a hard-coded threshold (in unit of bins):
      ! if time_remain/time_bin_duration < threshold the program terminates.

#ifdef MPI
      Use mpi
#endif

      logical, intent(out)                 :: prog_truncation
      real(kind=kind(0.d0)), intent(in)    :: cpu_max
      integer(kind=kind(0.d0)), intent(in) :: count_bin_start, count_bin_end
      integer, intent(in)                  :: group_comm
      real(kind=kind(0.d0))                :: count_alloc_end
      real(kind=kind(0.d0))                :: time_bin_duration,time_remain,bins_remain,threshold
#ifdef MPI
      real(kind=kind(0.d0))                :: bins_remain_mpi
      integer                              :: err_mpi, irank, isize, irank_g, isize_g
#endif
      threshold = 1.5d0
      prog_truncation = .false.

#ifdef MPI
      call mpi_comm_size(mpi_comm_world, isize, err_mpi)
      call mpi_comm_rank(mpi_comm_world, irank, err_mpi)
      call mpi_comm_size(group_comm, isize_g, err_mpi)
      call mpi_comm_rank(group_comm, irank_g, err_mpi)
#endif
      count_alloc_end   = count_CPU_start + cpu_max*3600*count_rate
      time_bin_duration = (count_bin_end-count_bin_start)/dble(count_rate)
      time_remain       = (count_alloc_end - count_bin_end)/dble(count_rate)
      if (count_bin_end .lt. count_bin_start) then ! the counter has wrapped around
         time_bin_duration = (count_max+count_bin_end-count_bin_start)/dble(count_rate)
         time_remain       = (count_alloc_end - count_bin_end-count_max)/dble(count_rate)
      endif
      bins_remain       = time_remain/time_bin_duration

#ifdef MPI
#ifdef PARALLEL_PARAMS
      call mpi_reduce(bins_remain,bins_remain_mpi,1,mpi_double_precision,mpi_sum,0,group_comm,err_mpi)
      if (irank_g .eq. 0) bins_remain_mpi = bins_remain_mpi/isize_g
      call mpi_bcast(bins_remain_mpi,1, mpi_double_precision,0, group_comm,err_mpi)
#else
      call mpi_reduce(bins_remain,bins_remain_mpi,1,mpi_double_precision,mpi_sum,0,mpi_comm_world,err_mpi)
      if (irank .eq. 0) bins_remain_mpi = bins_remain_mpi/isize
      call mpi_bcast(bins_remain_mpi,1, mpi_double_precision,0, mpi_comm_world,err_mpi)
#endif
      bins_remain = bins_remain_mpi
#endif
      if (bins_remain .lt. threshold) prog_truncation = .true.
      end subroutine make_truncation

    end module control
